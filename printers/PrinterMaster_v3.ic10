# Semiprime's Printer Master
# See https://github.com/drclaw1188/stationeers_ic10/tree/main/printers for details
alias VendingMachine d0
alias Autolathe d1
alias PipeBender d2
alias Electronics d3
alias Tools d4
define MINIMUMINGOT 50
define LED 1944485013
define DCUTERIGHT 163728359
define DCUTELEFT 648608238
alias Ingot r11

start:
yield
sb DCUTERIGHT Setting 0
sb DCUTELEFT Setting 0
sb DCUTERIGHT SettingOutput 0
sb DCUTELEFT SettingOutput 0
sb DCUTERIGHT On 1
sb DCUTELEFT On 1
sbn DCUTERIGHT HASH("DC:Autolathe") Mode 0
sbn DCUTELEFT HASH("DC:Autolathe") Mode 0
sbn DCUTERIGHT HASH("DC:PipeBender") Mode 0
sbn DCUTELEFT HASH("DC:PipeBender") Mode 0
sbn DCUTERIGHT HASH("DC:Electronics") Mode 0
sbn DCUTELEFT HASH("DC:Electronics") Mode 0
sbn DCUTERIGHT HASH("DC:Tools") Mode 0
sbn DCUTELEFT HASH("DC:Tools") Mode 0
sb LED Color 2
move r9 1
jal CheckPrinter
move r9 2
jal CheckPrinter
move r9 3
jal CheckPrinter
move r9 4
jal CheckPrinter
j start

CheckPrinter:
move Ingot 0
lr r0 dr9 Contents HASH("Iron")
slt r0 r0 MINIMUMINGOT
select Ingot r0 HASH("ItemIronIngot") Ingot
lr r0 dr9 Required HASH("Iron")
select Ingot r0 HASH("ItemIronIngot") Ingot
lr r0 dr9 Contents HASH("Copper")
slt r0 r0 MINIMUMINGOT
select Ingot r0 HASH("ItemCopperIngot") Ingot
lr r0 dr9 Required HASH("Copper")
select Ingot r0 HASH("ItemCopperIngot") Ingot
lr r0 dr9 Contents HASH("Gold")
slt r0 r0 MINIMUMINGOT
select Ingot r0 HASH("ItemGoldIngot") Ingot
lr r0 dr9 Required HASH("Gold")
select Ingot r0 HASH("ItemGoldIngot") Ingot
lr r0 dr9 Required HASH("Lead")
select Ingot r0 HASH("ItemLeadIngot") Ingot
lr r0 dr9 Required HASH("Nickel")
select Ingot r0 HASH("ItemNickelIngot") Ingot
lr r0 dr9 Contents HASH("Silicon")
slt r0 r0 MINIMUMINGOT
select Ingot r0 HASH("ItemSiliconIngot") Ingot
lr r0 dr9 Required HASH("Silicon")
select Ingot r0 HASH("ItemSiliconIngot") Ingot
lr r0 dr9 Required HASH("Silver")
select Ingot r0 HASH("ItemSilverIngot") Ingot
lr r0 dr9 Required HASH("Constantan")
select Ingot r0 HASH("ItemConstantanIngot") Ingot
lr r0 dr9 Required HASH("Electrum")
select Ingot r0 HASH("ItemElectrumIngot") Ingot
lr r0 dr9 Required HASH("Invar")
select Ingot r0 HASH("ItemInvarIngot") Ingot
lr r0 dr9 Required HASH("Solder")
select Ingot r0 HASH("ItemSolderIngot") Ingot
lr r0 dr9 Contents HASH("Steel")
slt r0 r0 MINIMUMINGOT
select Ingot r0 HASH("ItemSteelIngot") Ingot
lr r0 dr9 Required HASH("Steel")
select Ingot r0 HASH("ItemSteelIngot") Ingot
lr r0 dr9 Required HASH("Astroloy")
select Ingot r0 HASH("ItemAstroloyIngot") Ingot
lr r0 dr9 Required HASH("Hastelloy")
select Ingot r0 HASH("ItemHastelloyIngot") Ingot
lr r0 dr9 Required HASH("Inconel")
select Ingot r0 HASH("ItemInconelIngot") Ingot
lr r0 dr9 Required HASH("Stellite")
select Ingot r0 HASH("ItemStelliteIngot") Ingot
lr r0 dr9 Required HASH("Waspaloy")
select Ingot r0 HASH("ItemWaspaloyIngot") Ingot
beqz Ingot ra
s db Setting Ingot
sb LED Color 5
move r5 2
FindIngot:
ls r0 VendingMachine r5 OccupantHash
beq r0 Ingot DeliverIngot
add r5 r5 1
blt r5 102 FindIngot
sb LED Color 4
sleep 5
j ra
DeliverIngot:
seq r1 r9 1
sbn DCUTERIGHT HASH("DC:Autolathe") Mode r1
sbn DCUTELEFT HASH("DC:Autolathe") Mode r1
seq r1 r9 2
sbn DCUTERIGHT HASH("DC:PipeBender") Mode r1
sbn DCUTELEFT HASH("DC:PipeBender") Mode r1
seq r1 r9 3
sbn DCUTERIGHT HASH("DC:Electronics") Mode r1
sbn DCUTELEFT HASH("DC:Electronics") Mode r1
seq r1 r9 4
sbn DCUTERIGHT HASH("DC:Tools") Mode r1
sbn DCUTELEFT HASH("DC:Tools") Mode r1
l r2 dr9 ImportCount
s VendingMachine RequestHash 0
yield
s VendingMachine RequestHash Ingot
WaitForDelivery:
sb LED Color 11
yield
l r0 dr9 ImportCount
beq r0 r2 WaitForDelivery
yield
j ra
