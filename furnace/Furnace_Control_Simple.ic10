# Madcat's advanced furnace control (simple version)
alias HotPump d0
alias ColdPump d1
alias ExhaustPump d2
alias OverrideLever d3
alias EngageLever d4
alias Dial d5

define FURNACE 545937711
define EMPTYPRESS 10
define UNSAFEPRESS 59000
define SAFEPRESS 58000
define STACKSIZE 85
define MININGOTSTACK 50
define MAXTANKPRESS 20000

alias CurTemp r8
alias CurPress r9
alias IngotHash r10
alias MinTemp r11
alias MaxTemp r12
alias MinPress r13
alias MaxPress r14
alias TargetPress r15

start:
yield
lb CurPress FURNACE Pressure Maximum
lb CurTemp FURNACE Temperature Maximum
bgt CurPress UNSAFEPRESS SafetyFirst

l sp Dial Setting
add sp sp 1
mul sp sp 5
pop IngotHash
pop MaxPress
pop MinPress
pop MaxTemp
pop MinTemp
s db Setting IngotHash
l r0 OverrideLever Setting
bnez r0 start
l r0 EngageLever Setting
beqz r0 DisabledMode

sb FURNACE On 1
sb FURNACE SettingInput 100
sb FURNACE SettingOutput 0
s ColdPump On 1
s HotPump On 1
s ExhaustPump On 1
s ColdPump Setting 0
s HotPump Setting 0
s ExhaustPump Setting 0

lb r0 FURNACE RecipeHash Average
seq r1 r0 IngotHash
lb r0 FURNACE Reagents Average
sge r0 r0 MININGOTSTACK
and r0 r0 r1
sb FURNACE Open r0
breqz r0 2
yield

select r0 TargetPress TargetPress MaxPress
bgt CurPress r0 DecreasePress
move TargetPress 0
blt CurPress MinPress IncreasePress
bgt CurTemp MaxTemp DecreaseTemp
blt CurTemp MinTemp IncreaseTemp
j start

DisabledMode:
s ColdPump On 0
s HotPump On 0
s ExhaustPump On 0
sb FURNACE SettingOutput 100
sgt r0 CurPress EMPTYPRESS
sb FURNACE On r0
j start

SafetyFirst:
s ColdPump On 0
s HotPump On 0
s ExhaustPump On 0
sb FURNACE SettingOutput 100
yield
lb CurPress FURNACE Pressure Maximum
bgt CurPress SAFEPRESS SafetyFirst
j start

DecreasePress:
sub r0 MaxPress MinPress
div r0 r0 2
sub TargetPress MaxPress r0
sub r0 CurPress TargetPress
div r0 r0 CurPress
mul r0 r0 100
max r0 r0 5
sb FURNACE SettingOutput r0
j start
DecreaseTemp:
sub r0 CurTemp MaxTemp
div r0 r0 CurTemp
mul r0 r0 100
max r0 r0 5
s ColdPump Setting r0
j start
IncreaseTemp:
sub r0 MinTemp CurTemp
div r0 r0 CurTemp
mul r0 r0 100
max r0 r0 5
s HotPump Setting r0
j start
IncreasePress:
sub r0 MinPress CurPress
div r0 r0 CurPress
mul r0 r0 100
max r0 r0 5
s ExhaustPump Setting r0
j start
