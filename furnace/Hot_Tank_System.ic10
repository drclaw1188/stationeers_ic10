alias Furnace d0
alias HotTank d1
alias HotPump d2
alias FuelPump d3
alias SafetyValve d4

alias CurTemp r8
alias CurPress r9
alias HotTemp r10
alias HotPress r11

define MINHOTTEMP 2000
define MINHOTPRESS 5000
define MAXHOTPRESS 20000
define MINFURPRESS 2000
define EMPTYPRESS 10
define UNSAFEPRESS 59000
define SAFEPRESS 58000

s HotPump Setting 100
s FuelPump Setting 25

start:
s db Setting 0
yield

l CurPress Furnace Pressure
l CurTemp Furnace Temperature
l HotPress HotTank Pressure
l HotTemp HotTank Temperature

s FuelPump On 0
s HotPump On 0
s SafetyValve On 0
bgtal CurPress UNSAFEPRESS SafetyFirst

slt r0 HotPress MINHOTPRESS
slt r1 HotTemp MINHOTTEMP
or r0 r0 r1
slt r1 HotPress MAXHOTPRESS
and r0 r0 r1

beqz r0 start

HeatUp:
s db Setting 1
yield
l CurPress Furnace Pressure
l CurTemp Furnace Temperature

bgtal CurPress UNSAFEPRESS SafetyFirst

s FuelPump On 1
s Furnace Activate 1

slt r0 CurPress MINFURPRESS
slt r1 CurTemp MINHOTTEMP
or r0 r0 r1

bnez r0 HeatUp

s FuelPump On 0
s HotPump On 1

EmptyTank:
s db Setting 2
yield
l CurPress Furnace Pressure
bgt CurPress EMPTYPRESS EmptyTank

s HotPump On 0

j start

SafetyFirst:
s db Setting 10
s HotPump On 0
s FuelPump On 0
s SafetyValve On 1
yield
l CurPress Furnace Pressure
bgt CurPress SAFEPRESS SafetyFirst
j ra
