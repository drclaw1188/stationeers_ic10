alias VendingMachine d0
alias FurnaceIC d1
alias ControlChute d2
alias RequestMem d3

define STACKSIZE 119
define FURNACE 545937711
define BUTTON 491845673
define LED 1944485013
define STEEL -654790771

alias MissingOre r4
alias SlotToCheck r5
alias IngotRequested r6
alias OreRequested r7
alias OreQuantity r8
alias Ore1 r9
alias Ore1Q r10
alias Ore2 r11
alias Ore2Q r12
alias Ore3 r13
alias Ore3Q r14
alias ItemCount r15
move IngotRequested 0
move MissingOre 0

main:
s db Setting MissingOre
select r0 MissingOre 4 2
sb LED Color r0
yield

l IngotRequested RequestMem Setting
move MissingOre 0
move OreRequested STEEL
move OreQuantity 4
jal CountOre
select IngotRequested MissingOre STEEL IngotRequested
bnez IngotRequested DispenseOre

DispenseOre:
s db Setting IngotRequested
move sp STACKSIZE
findIngot:
pop Ore3Q
pop Ore3
pop Ore2Q
pop Ore2
pop Ore1Q
pop Ore1
pop r0
bne r0 IngotRequested findIngot

sb LED Color 5
move OreRequested Ore1
move OreQuantity Ore1Q
jal CountOre
move OreRequested Ore2
move OreQuantity Ore2Q
jal CountOre
move OreRequested Ore3
move OreQuantity Ore3Q
jal CountOre
bnez MissingOre main

sb LED Color 3
move OreRequested Ore1
move OreQuantity Ore1Q
jal VendOre
move OreRequested Ore2
move OreQuantity Ore2Q
jal VendOre
move OreRequested Ore3
move OreQuantity Ore3Q
jal VendOre

s FurnaceIC Setting IngotRequested
lb r0 FURNACE ExportCount Maximum
WaitForExport:
yield
lb r1 FURNACE ExportCount Maximum
beq r0 r1 WaitForExport
s FurnaceIC Setting 0

WaitForMem:
yield
l r0 RequestMem Setting
bnez r0 WaitForMem
j main

VendOre:
beqz OreRequested ra
lb r1 FURNACE ImportCount Maximum
s VendingMachine RequestHash 0
yield # Above line is workaround for game bug
s VendingMachine RequestHash OreRequested
WaitForFurnImport:
yield
lb r0 FURNACE ImportCount Maximum
beq r0 r1 WaitForFurnImport
sub OreQuantity OreQuantity 1
bnez OreQuantity VendOre
j ra

CountOre:
move ItemCount 0
beqz OreRequested ra
move SlotToCheck 2
ContinueChecking:
ls r1 VendingMachine SlotToCheck OccupantHash
brne r1 OreRequested 2
add ItemCount ItemCount 1
add SlotToCheck SlotToCheck 1
bge ItemCount OreQuantity ra
ble SlotToCheck 102 ContinueChecking
move MissingOre OreRequested
j ra
