alias VendingMachine d0
alias UsedSlotsDisplay d1
alias QueryMemory1 d2
alias ResponseMemory1 d3
alias QueryMemory2 d4
alias ResponseMemory2 d5

define LASTSLOT 101

alias QuantityToAdd r15
alias QuantityToSubtract r14
alias ItemHash r13
alias ItemCount r12
alias UsedSlots r11
alias CurSlot r10

Setup:
move ItemCount 0
move UsedSlots 0
move CurSlot 1
move QuantityToSubtract 0
s db Setting 50
ContinueCounting:
add CurSlot CurSlot 1
bgt CurSlot LASTSLOT Main
ls ItemHash VendingMachine CurSlot OccupantHash
beqz ItemHash ContinueCounting
add UsedSlots UsedSlots 1
ls QuantityToAdd VendingMachine CurSlot Quantity
jal UpdateStack
j ContinueCounting

Main:
yield
s db Setting 100
s UsedSlotsDisplay Setting UsedSlots

ls ItemHash VendingMachine 0 OccupantHash
breqz ItemHash 5
add UsedSlots UsedSlots 1
move QuantityToSubtract 0
ls QuantityToAdd VendingMachine 0 Quantity
jal UpdateStack
ls ItemHash VendingMachine 1 OccupantHash
breqz ItemHash 5
sub UsedSlots UsedSlots 1
move QuantityToAdd 0
ls QuantityToSubtract VendingMachine 1 Quantity
jal UpdateStack

WaitForImportExport:
yield
ls r0 VendingMachine 0 OccupantHash
ls r1 VendingMachine 1 OccupantHash
or r0 r0 r1
bnez r0 WaitForImportExport

l ItemHash QueryMemory1 Setting
# breqz ItemHash 3
move r9 3
jal GetItemCount
l ItemHash QueryMemory2 Setting
# breqz ItemHash 3
move r9 5
jal GetItemCount

j Main

GetItemCount:
brnez ItemHash 3
s dr9 Setting -1
j ra
mul sp ItemCount 2
FindItem1:
pop r0
pop r1
beq ItemHash r0 FoundItem
bgtz sp FindItem1
move r1 0
FoundItem:
s dr9 Setting r1
j ra

UpdateStack:
s db Setting ItemHash
mul sp ItemCount 2
beqz sp AddItem
FindItem2:
pop r0
pop r1
beq ItemHash r0 UpdateItem
bgtz sp FindItem2
AddItem:
mul sp ItemCount 2
sub r0 QuantityToAdd QuantityToSubtract
push r0
push ItemHash
add ItemCount ItemCount 1
j ra
UpdateItem:
add r1 r1 QuantityToAdd
sub r1 r1 QuantityToSubtract
push r1
j ra
